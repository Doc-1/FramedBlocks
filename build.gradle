plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.124'
}

version = mod_version
group = 'xfacthd.framedblocks'

base {
    archivesName = 'FramedBlocks'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

sourceSets {
    main.resources {
        srcDir 'src/generated/resources'
    }

    datagen {
        java {
            srcDir "src/datagen/java"
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }

    test {
        java {
            srcDir "src/test/java"
        }
        resources {
            srcDir "src/test/resources"
        }
    }
}

neoForge {
    version = project.neoforge_version

    validateAccessTransformers = true

    interfaceInjectionData = files('src/main/resources/injected_interfaces.json')

    parchment {
        minecraftVersion = project.parchment_minecraft_version
        mappingsVersion = project.parchment_mappings_version
    }

    runs {
        configureEach {
            // systemProperty  'neoforge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            systemProperty 'neoforge.enabledGameTestNamespaces', 'framedblocks'
            systemProperty 'terminal.ansi', 'true'

            logLevel = org.slf4j.event.Level.DEBUG

            sourceSet = project.sourceSets.test
        }

        client {
            client()
            gameDirectory = project.file('run')
        }

        server {
            server()
            gameDirectory = project.file('run_server')

            programArgument '--nogui'
        }

        data {
            clientData()
            gameDirectory = project.file('run')

            programArguments.addAll(
                    '--mod', 'framedblocks',
                    '--all',
                    '--output', file('src/generated/resources/').getAbsolutePath(),
                    '--existing', file('src/main/resources/').getAbsolutePath()
            )

            sourceSet = project.sourceSets.datagen
        }

        gameTestServer {
            type = "gameTestServer"
            gameDirectory = project.file('run_gametest')
        }
    }

    mods {
        framedblocks {
            sourceSet(project.sourceSets.main)
            sourceSet(project.sourceSets.datagen)
            sourceSet(project.sourceSets.test)
        }
    }

    addModdingDependenciesTo(project.sourceSets.datagen)
    addModdingDependenciesTo(project.sourceSets.test)
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = 'CurseMaven'
                url = 'https://www.cursemaven.com'
            }
        }
        filter {
            includeGroup('curse.maven')
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'tterrag\'s maven'
                url = 'https://maven.tterrag.com/'
            }
        }
        filter {
            includeGroup('team.chisel.ctm')
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'Create maven'
                url = 'https://maven.createmod.net/'
            }
        }
        filter {
            includeGroup('com.simibubi.create')
            includeGroup('net.createmod.ponder')
            includeGroup('dev.engine-room.flywheel')
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'DevOS maven'
                url = 'https://mvn.devos.one/snapshots'
            }
        }
        filter {
            includeGroup('com.tterrag.registrate')
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'Jared\'s maven'
                url = 'https://maven.blamejared.com/'
            }
        }
        filter {
            includeGroup('org.embeddedt')
            includeGroup('mezz.jei')
            includeGroup('com.blamejared.searchables')
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'TerraformersMC maven'
                url = 'https://maven.terraformersmc.com/'
            }
        }
        filter {
            includeGroup('dev.emi')
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'ResourcefulBees maven'
                url = 'https://maven.resourcefulbees.com/repository/maven-public/'
            }
        }
        filter {
            includeGroup('com.teamresourceful')
            includeGroup('com.teamresourceful.resourcefullib')
            includeGroup('earth.terrarium.athena')
            includeGroup('earth.terrarium.chipped')
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'Fuzs Mod Resources'
                url = 'https://raw.githubusercontent.com/Fuzss/modresources/main/maven/'
            }
        }
        filter {
            includeGroup('fuzs.diagonalblocks')
        }
    }
}

// Sets up a dependency configuration called 'localRuntime'.
// This configuration should be used instead of 'runtimeOnly' to declare
// a dependency that will be present for runtime testing but that is
// "optional", meaning it will not be pulled by dependents of this mod.
// The dependency is so local that the used classpath must be from the
// sourceset specified in the run config and not a parent thereof
configurations {
    testRuntimeClasspath.extendsFrom localRuntime
}

dependencies {
    compileOnly("fuzs.diagonalblocks:diagonalblocks-neoforge:${diag_blocks_version}") { transitive = false }
    //localRuntime("curse.maven:puzzleslib-495476:${puzzles_lib_coord}")
    //localRuntime("curse.maven:diagonalfences-458048:${diagonal_fences_coord}")
    //localRuntime("curse.maven:diagonalwindows-891328:${diagonal_windows_coord}")

    //compileOnly("curse.maven:buildinggadgets-298187:${buildinggadgets_coord}")
    //localRuntime("curse.maven:buildinggadgets-298187:${buildinggadgets_coord}")

    compileOnly("mezz.jei:jei-${jei_mc_version}-common-api:${jei_version}")
    compileOnly("mezz.jei:jei-${jei_mc_version}-neoforge-api:${jei_version}")
    localRuntime("mezz.jei:jei-${jei_mc_version}-neoforge:${jei_version}")

    compileOnly("dev.emi:emi-neoforge:${emi_version}+${emi_mc_version}:api")
    //localRuntime("dev.emi:emi-neoforge:${emi_version}+${emi_mc_version}")

    compileOnly("com.simibubi.create:create-${create_mc_version}:${create_version}-${create_build_number}") { transitive = false }
    compileOnly("net.createmod.ponder:Ponder-NeoForge-${create_mc_version}:${ponder_version}")
    // Only needed when Create and Ponder are switched to implementation
    //localRuntime("dev.engine-room.flywheel:flywheel-neoforge-${create_mc_version}:${flywheel_version}")
    //localRuntime("com.tterrag.registrate:Registrate:${registrate_version}")

    //localRuntime("curse.maven:moonlight-499980:${moonlight_coord}")
    compileOnly("curse.maven:amendments-896746:${amendments_coord}")
    //localRuntime("curse.maven:amendments-896746:${amendments_coord}")

    compileOnly("org.appliedenergistics:appliedenergistics2:${ae2_version}:api")
    //localRuntime("org.appliedenergistics:appliedenergistics2:${ae2_version}")

    compileOnly("curse.maven:jade-324717:${jade_coord}")
    //localRuntime("curse.maven:jade-324717:${jade_coord}")

    compileOnly("curse.maven:atlasviewer-633577:${atlasviewer_coord}")
    localRuntime("curse.maven:atlasviewer-633577:${atlasviewer_coord}")

    compileOnly("com.blamejared.searchables:Searchables-neoforge-${searchables_mc_version}:${searchables_version}")
    //localRuntime("com.blamejared.searchables:Searchables-neoforge-${searchables_mc_version}:${searchables_version}")

    compileOnly("curse.maven:additionalplacements-674852:${additionalplacements_coord}")
    //localRuntime("curse.maven:additionalplacements-674852:${additionalplacements_coord}")
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version: minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neoforge_version: neoforge_version,
            neoforge_version_range: neoforge_version_range,
            mod_version: mod_version,
            jei_version: jei_version,
            emi_version: emi_version,
            diag_blocks_version: diag_blocks_version,
            buildinggadgets_version: buildinggadgets_version,
            atlasviewer_version: atlasviewer_version,
            amendments_version: amendments_version,
            create_version: create_version,
            ae2_version: ae2_version,
            jade_version: jade_version,
            searchables_version: searchables_version,
            additionalplacements_version: additionalplacements_version
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

processTestResources {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
}

jar {
    exclude("/.cache")
}

publishing {
    publications {
        register ('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
    options.compilerArgs << "-Xmaxerrs" << "10000"
}
