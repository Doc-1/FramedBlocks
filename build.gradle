plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev.legacyforge' version '2.0.79'
}

version = mod_version
group = 'xfacthd.framedblocks'

base {
    archivesName = 'FramedBlocks'
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

legacyForge {
    version = "1.20.1-${forge_version}"

    runs {
        configureEach {
            systemProperty 'forge.enabledGameTestNamespaces', 'framedblocks'
            systemProperty 'terminal.ansi', 'true'

            logLevel = org.slf4j.event.Level.DEBUG

            sourceSet = project.sourceSets.test
        }

        client {
            client()
            gameDirectory = project.file('run')
        }

        server {
            server()
            gameDirectory = project.file('run_server')

            programArgument '--nogui'
        }

        data {
            data()
            gameDirectory = project.file('run')

            programArguments.addAll(
                    '--mod', 'framedblocks',
                    '--all',
                    '--output', file('src/generated/resources/').getAbsolutePath(),
                    '--existing', file('src/main/resources/').getAbsolutePath(),
                    '--existing-mod', 'ae2'
            )
        }

        gameTestServer {
            type = "gameTestServer"
            gameDirectory = project.file('run_gametest')
        }
    }

    mods {
        framedblocks {
            sourceSet sourceSets.main
            sourceSet sourceSets.test
        }
    }

    addModdingDependenciesTo(project.sourceSets.test)

    parchment {
        minecraftVersion = "${minecraft_version}"
        mappingsVersion = "${mapping_version}"
    }
}

sourceSets {
    main.resources {
        srcDir 'src/generated/resources'
    }

    test {
        java {
            srcDir "src/test/java"
        }
        resources {
            srcDir "src/test/resources"
        }
    }
}

repositories {
    maven {
        name = 'MinecraftForge'
        url = 'https://maven.minecraftforge.net/'
    }
    maven {
        name = 'CurseMaven'
        url = 'https://www.cursemaven.com'
        content {
            includeGroup('curse.maven')
        }
    }
    maven {
        name = "tterrag's maven"
        url = "https://maven.tterrag.com/"
        content {
            includeGroup('team.chisel.ctm')
            includeGroup('com.tterrag.registrate')
        }
    }
    maven {
        name = 'Create maven'
        url = 'https://maven.createmod.net/'
        content {
            includeGroup('com.simibubi.create')
            includeGroup('net.createmod.ponder')
            includeGroup('dev.engine-room.flywheel')
        }
    }
    maven {
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        name = "Shedaniel's maven"
        url = "https://maven.shedaniel.me/"
    }
    mavenCentral()
    mavenLocal()
}

dependencies {
    modCompileOnly "team.chisel.ctm:CTM:1.20.1-1.1.8+4"

    //modRuntimeOnly "curse.maven:puzzleslib-495476:4621178" //Only needed when Diagonal Fences or Diagonal Windows is switched to implementation
    modCompileOnly "curse.maven:diagonalfences-458048:4611775"
    modCompileOnly "curse.maven:diagonalwindows-891328:4660584"

    modCompileOnly "curse.maven:buildinggadgets-298187:3829297"

    modImplementation ("xfacthd.contex:ConnectedTextures:2.1:slim") { transitive = false }

    modImplementation "curse.maven:athena-841890:5176879"
    //modRuntimeOnly "curse.maven:resfullib-570073:5210240"
    //modRuntimeOnly "curse.maven:chipped-456956:5270039"

    modCompileOnly "curse.maven:xycraft_core-653786:4788862"
    modCompileOnly "curse.maven:xycraft_world-653789:4788863"

    modCompileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    modCompileOnly "mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"
    //modRuntimeOnly "mezz.jei:jei-${minecraft_version}-forge:${jei_version}"

    // Switch Arch, Cloth and REI-forge to impl for runtime testing
    modCompileOnly "dev.architectury:architectury-forge:${arch_version}"
    modCompileOnly "me.shedaniel.cloth:cloth-config-forge:${cloth_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-forge:${rei_version}"
    //modRuntimeOnly "curse.maven:reipc-521393:4723408"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-forge:${rei_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-forge:${rei_version}"
    modCompileOnly "me.shedaniel:REIPluginCompatibilities-forge-annotations:${reipcanno_version}"

    modCompileOnly "dev.emi:emi-forge:${emi_version}+${minecraft_version}:api"
    modRuntimeOnly "dev.emi:emi-forge:${emi_version}+${minecraft_version}"

    //modRuntimeOnly "curse.maven:spark-361579:3767286"

    //modRuntimeOnly "curse.maven:rubidium-574856:4684247"
    //modRuntimeOnly "org.embeddedt:embeddium-${embeddium_mc_version}:${embeddium_version}+mc${embeddium_mc_version}"
    //modRuntimeOnly "curse.maven:oculus-581495:5299671"
    //modRuntimeOnly "curse.maven:starlight-forge-526854:4631193"

    modCompileOnly ("com.simibubi.create:create-${create_mc_version}:${create_version}-${create_build_number}:slim") { transitive = false }
    modCompileOnly "net.createmod.ponder:Ponder-Forge-${create_mc_version}:${ponder_version}"
    modCompileOnly "dev.engine-room.flywheel:flywheel-forge-api-${create_mc_version}:${flywheel_version}-${flywheel_build_number}"
    // Only needed when Create and Ponder are switched to implementation
    //modRuntimeOnly "dev.engine-room.flywheel:flywheel-forge-${create_mc_version}:${flywheel_version}-${flywheel_build_number}"
    //modRuntimeOnly "com.tterrag.registrate:Registrate:${registrate_version}"

    modCompileOnly "curse.maven:moonlight-499980:4621773"
    modCompileOnly "curse.maven:supplementaries-412082:4622527"

    //modRuntimeOnly "curse.maven:corelib-454372:4927036"
    //modRuntimeOnly "curse.maven:fusion-854949:5005130"
    //modRuntimeOnly "curse.maven:conglass-383129:5002550"

    modCompileOnly "curse.maven:nocubes-309664:3944569"

    modImplementation "curse.maven:guidebook-253874:4593765"

    modCompileOnly "curse.maven:jade-324717:5072729"

    modCompileOnly "curse.maven:atlasviewer-633577:4762356"

    additionalRuntimeClasspath(group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.1.1')
    jarJar(implementation(group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.1.1')) {
        transitive(false)
    }

    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:0.2.0-rc.4"))
    jarJar(implementation("io.github.llamalad7:mixinextras-forge:0.2.0-rc.4")) {
        version {
            strictly '[0.2.0-rc.4,)'
            prefer '0.2.0-rc.4'
        }
    }

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    compileOnly 'org.jetbrains:annotations:26.0.1' // Needed because MDG legacy doesn't inject it automatically
}

mixin {
    add sourceSets.main, "framedblocks.refmap.json"
    config "framedblocks.mixin.json"
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
def resourceTargets = ['META-INF/mods.toml', 'pack.mcmeta']
def replaceProperties = [
        minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
        forge_version: forge_version, forge_version_range: forge_version_range,
        loader_version_range: loader_version_range, mod_version: mod_version
]

processResources {
    //exclude("framedblocks.mixin.json")

    inputs.properties replaceProperties
    replaceProperties.put 'project', project

    filesMatching(resourceTargets) {
        expand replaceProperties
    }
}

processTestResources {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
}

// Example for how to get properties into the manifest for reading by the runtime..
tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "Specification-Title"     : "framedblocks",
                "Specification-Vendor"    : "XFactHD",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "XFactHD",
                "MixinConfigs"            : "framedblocks.mixin.json"
        ])
    }

    // The settings below make sure that your build is reproducible
    // More information at https://docs.gradle.org/current/userguide/working_with_files.html#sec:reproducible_archives
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
